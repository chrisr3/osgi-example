import aQute.bnd.gradle.Bundle
import aQute.bnd.gradle.TestOSGi

plugins {
    id 'biz.aQute.bnd.builder'
    id 'org.jetbrains.kotlin.jvm'
    id 'java-library'
}

sourceSets {
    integrationTest {
        kotlin {
            srcDirs = [ 'src/integration-test/kotlin' ]
        }
    }
}

configurations {
    testCompileOnly.extendsFrom compileOnly
    integrationTestApi.extendsFrom testApi
    integrationTestCompileOnly.extendsFrom testCompileOnly
    integrationTestImplementation.extendsFrom testImplementation

    [ compileClasspath, testCompileClasspath, integrationTestCompileClasspath ].forEach {
        it.exclude module: 'kotlin-stdlib-jdk8'
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-osgi-bundle:$kotlin_version"
    api "org.jetbrains:annotations:$jetbrains_annotations_version"
    api project(':api')

    compileOnly "org.osgi:osgi.annotation:$osgi_version"
    compileOnly "org.osgi:osgi.core:$osgi_version"
    compileOnly "org.osgi:org.osgi.service.component.annotations:$osgi_component_annotations_version"
    implementation "org.osgi:org.osgi.service.log:$osgi_log_version"
    implementation "org.osgi:org.osgi.util.pushstream:$osgi_pushstream_version"
    implementation "org.osgi:osgi.promise:$osgi_version"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
    testRuntimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"

    integrationTestRuntimeOnly "biz.aQute.bnd:biz.aQute.tester.junit-platform:$bnd_version"
    integrationTestRuntimeOnly "org.apache.felix:org.apache.felix.framework:$felix_version"
    integrationTestRuntimeOnly "org.apache.felix:org.apache.felix.scr:$felix_scr_version"
    integrationTestRuntimeOnly "org.apache.felix:org.apache.felix.log:$felix_log_version"

    testImplementation "biz.aQute.bnd:biz.aQute.tester:$bnd_version"
}

//compile 'org.apache.felix:org.apache.felix.main:5.4.0',
//        'org.osgi:org.osgi.compendium:5.0.0',
//        'org.apache.felix:org.apache.felix.dependencymanager:4.2.0'
//


def testingBundle = tasks.register('testingBundle', Bundle) {
    archiveClassifier = 'tests'
    from sourceSets.integrationTest.output

    manifest {
        attributes('Test-Cases': '\${classes;HIERARCHY_INDIRECTLY_ANNOTATED;org.junit.platform.commons.annotation.Testable;CONCRETE}')
    }
}

tasks.register('testOSGi', TestOSGi) {
    bndrun = 'boop'
}

artifacts {
    archives testingBundle.flatMap { it.archiveFile }
}
