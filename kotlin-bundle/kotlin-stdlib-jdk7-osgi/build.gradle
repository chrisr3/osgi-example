plugins {
    id 'net.corda.plugins.publish-utils'
    id 'com.jfrog.artifactory'
    id 'biz.aQute.bnd.builder'
    id 'java-library'
}

description 'The Kotlin Stdlib JDK7 extension as an OSGi bundle.'

configurations {
    osgi.extendsFrom runtimeClasspath
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-osgi-bundle:$kotlin_version"
    api "org.jetbrains:annotations:$jetbrains_annotations_version"
    bundle "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

def jar = tasks.named('jar', Jar) {
    archiveVersion = kotlin_version
    archiveBaseName = 'kotlin-stdlib-jdk7-osgi'

    bnd kotlinProvider

    bnd """\
-fixupmessages "Classes found in the wrong directory"; restrict:=error; is:=warning
Bundle-SymbolicName: net.corda.kotlin-stdlib-jdk7.osgi-bundle
Bundle-Name: Kotlin JDK7 classes
"""
}

tasks.named('sourceJar', Jar) {
    from 'README.md'
    include 'README.md'
}

tasks.named('javadocJar', Jar) {
    from 'README.md'
    include 'README.md'
}

artifacts {
    osgi jar
}

publish {
    name jar.flatMap { it.archiveBaseName }
}
